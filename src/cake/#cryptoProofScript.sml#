(*
  Compose the crypto semantics theorem and the compiler correctness
  theorem with the compiler evaluation theorem to produce end-to-end
  correctness theorem that reaches final machine code.
*)
open preamble
     semanticsPropsTheory backendProofTheory arm8_configProofTheory (* x64_configProofTheory *)
     cryptoProgTheory cryptoCompileTheory

val _ = new_theory"cryptoProof";

val crypto_io_events_def = new_specification("crypto_io_events_def",["crypto_io_events"],
  crypto_semantics
  |> Q.GENL[`cl`,`fs`]
  |> SIMP_RULE bool_ss [SKOLEM_THM,GSYM RIGHT_EXISTS_IMP_THM]);

val (crypto_sem,crypto_output) = crypto_io_events_def |> SPEC_ALL |> UNDISCH |> CONJ_PAIR
val (crypto_not_fail,crypto_sem_sing) = MATCH_MP semantics_prog_Terminate_not_Fail crypto_sem |> CONJ_PAIR

val compile_correct_applied =
  MATCH_MP compile_correct crypto_compiled
  |> SIMP_RULE(srw_ss())[LET_THM,ml_progTheory.init_state_env_thm,GSYM AND_IMP_INTRO]
  |> C MATCH_MP crypto_not_fail
  |> C MATCH_MP x64_backend_config_ok
  |> REWRITE_RULE[crypto_sem_sing,AND_IMP_INTRO]
  |> REWRITE_RULE[Once (GSYM AND_IMP_INTRO)]
  |> C MATCH_MP (CONJ(UNDISCH x64_machine_config_ok)(UNDISCH x64_init_ok))
  |> DISCH(#1(dest_imp(concl x64_init_ok)))
  |> REWRITE_RULE[AND_IMP_INTRO]

val crypto_compiled_thm =
  CONJ compile_correct_applied crypto_output
  |> DISCH_ALL
  |> check_thm
  |> curry save_thm "crypto_compiled_thm";

val _ = export_theory();
